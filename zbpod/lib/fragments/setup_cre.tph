COPY ~%MOD_FOLDER%/creatures/deathslither_dracolich~ ~override~
COPY_EXISTING ~zbdrclch.CRE~ ~override~
    SAY 0x08 @15000
    SAY 0x0c @15000

COPY ~%MOD_FOLDER%/creatures/nephetel~ ~override~
COPY_EXISTING ~zspdnf16.CRE~ ~override~
    SAY 0x08 @15001
    SAY 0x0c @15001
    WRITE_ASCII SCRIPT_OVERRIDE ~ZSPDNFS~ #8
    WRITE_ASCII DEATHVAR ~ZSPDNF16~ #32
    WRITE_ASCII DIALOG ~ZBNEPH~ #8

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/creatures/#scripts/zspdnfs.baf~

COPY ~%MOD_FOLDER%/creatures/dennaton~ ~override~
COPY_EXISTING ~ohbdenna.cre~ ~override~
    SAY 0x08 @15002
    SAY 0x0c @15002

COPY ~%MOD_FOLDER%/creatures/couatl~ ~override~
COPY_EXISTING ~zbcouatl.cre~ ~override~
    SAY 0x08 @15003
    SAY 0x0c @15003
    WRITE_LONG 0x28 (IDS_OF_SYMBOL (~animate~ ~P_COUATL~))

COPY ~%MOD_FOLDER%/creatures/stirv~ ~override~
COPY_EXISTING ~ohbstirv.cre~ ~override~
    SAY 0x08 @15004
    SAY 0x0c @15004

COPY ~%MOD_FOLDER%/creatures/mutvampworg~ ~override~

// Create 5 unique instances of vampiric worgs
ACTION_FOR_EACH wolf IN 
        1
        2
        3
        4
    BEGIN
        COPY_EXISTING ~ohbmena1.cre~ ~override/mowlf0%wolf%.cre~
            SAY 0x08 @15005
            SAY 0x0c @15005
            WRITE_BYTE 0x24  80  // Current HP
            WRITE_BYTE 0x26  80  // Maximum HP
            WRITE_BYTE 0x238 17  // Strength
            WRITE_BYTE 0x23c 18  // Dexterity
            WRITE_BYTE 0x53   3  // 2 APR Base
            //WRITE_BYTE 0x60  15  // Slashing resistance
            //WRITE_BYTE 0x61  15  // Crushing resistance
            //WRITE_BYTE 0x62  15  // Piercing resistance
            //WRITE_BYTE 0x63  15  // Missile resistance
            WRITE_ASCII 0x258 ~zbdom~        #8 // Add Race Script
            WRITE_ASCII 0x268 ~momatk01~     #8 // Add Default Script
            WRITE_ASCII 0x280 ~mowlf0%wolf%~ #8 // Script Name
            REMOVE_CRE_ITEM minhp1
            WRITE_ASCII SCRIPT_OVERRIDE ~ohbmeng~
            WRITE_LONG 0x28 (IDS_OF_SYMBOL (~animate~ ~P_RED_WORG~))
END    

// All moosey moose related code goes here
COPY ~%MOD_FOLDER%/creatures/mutdiremoose~ ~override~
COPY_EXISTING "zbmoose1.spl" "override" // first we tackle the stomping effect
    LPF ALTER_EFFECT
        INT_VAR
            match_opcode = 139 // we map this altering to opcode 139 (display string)
            parameter1 = RESOLVE_STR_REF(@18001) // string tlk index; RESOLVE_STR_REF injects the string in the tlk and returns index
    END

COPY_EXISTING "zbmoose2.spl" "override" // next the unconsciousness effect
    LPF ALTER_EFFECT
        INT_VAR
            match_opcode = 139 // we map this altering to opcode 139 (display string)
            parameter1 = RESOLVE_STR_REF(@18002) // string tlk index; RESOLVE_STR_REF injects the string in the tlk and returns index // substitute later for a @xxxx reference
    END

COMPILE "%MOD_FOLDER%/creatures/#scripts/zbmostmp.baf" // compile the script that makes the moose stomp selectively

// Create 3 unique instances of moose
ACTION_FOR_EACH moose IN 
        1
        2
        3
    BEGIN
        COPY_EXISTING ~ohbmena2.cre~ ~override/momoos0%moose%.cre~
            SAY 0x08 @15006 // inject into name field (dangerous moose)
            SAY 0x0c @15006 // inject into creature tooltip field (dangerous moose)
            WRITE_BYTE 0x60  40  // Slashing resistance
            WRITE_BYTE 0x61  40  // Crushing resistance
            WRITE_BYTE 0x62  40  // Piercing resistance
            WRITE_BYTE 0x63  40  // Missile resistance
            WRITE_ASCII 0x260 ~zbmostmp~       #8  // inject script name to general script field; #8 is the length of the string
            WRITE_ASCII 0x268 ~momatk01~       #8 // Add Default Script
            WRITE_ASCII 0x280 ~momoos0%moose%~ #8 // Script Name
            REMOVE_CRE_ITEM minhp1
            WRITE_ASCII SCRIPT_OVERRIDE ~ohbmeng~
END
// end of moosey moosey code

COPY ~%MOD_FOLDER%/creatures/mutdirebear~ ~override~

// Create 2 unique instances of bears
ACTION_FOR_EACH bear IN 
        1
        2
        3
    BEGIN
        COPY_EXISTING ~ohbmena3.cre~ ~override/mobear0%bear%.cre~
            SAY 0x08 @15007
            SAY 0x0c @15007
            REMOVE_CRE_ITEM minhp1
            WRITE_ASCII SCRIPT_OVERRIDE ~ohbmeng~
            WRITE_ASCII 0x258 ~mobear01~      #8 // Add Race Script
            WRITE_ASCII 0x268 ~momatk01~      #8 // Add Default Script
            WRITE_ASCII 0x280 ~mobear0%bear%~ #8 // Script Name
END 

// start of dire lion related code
/*
COPY ~%MOD_FOLDER%/creatures/mutdirelion~ ~override~
COPY_EXISTING ~ohbmena4.cre~ ~override~
    SAY 0x08 @15008
    SAY 0x0c @15008
    REMOVE_CRE_ITEM minhp1
    WRITE_ASCII SCRIPT_OVERRIDE ~ohbmeng~

COPY_EXISTING "zblion1.spl" "override" // we tackle the knocking over effect
    LPF ALTER_EFFECT
        INT_VAR
            match_opcode = 139 // we map this altering to opcode 139 (display string)
            parameter1 = RESOLVE_STR_REF(@18003) // string tlk index; RESOLVE_STR_REF injects the string in the tlk and returns index
    END
*/
// end of dire lion related code

//-------------------------------------------------------------------------
// Displacer Beast - Animations provided by Acifer, THANK YOU!
LAF FIND_FREE_ANIM_SLOT INT_VAR slotMin = 0x7F3D slotMax = slotMin + 0xff RET slot END
ACTION_IF (slot <= 0) BEGIN
  FAIL ~No free creature animation slot found.~
END

LAF TO_HEX_NUMBER INT_VAR value = slot minDigits = 4 RET hexNumber END
APPEND ~animate.ids~ ~0x%hexNumber% AC#DISPLACER_BEAST~ UNLESS ~AC#DISPLACER_BEAST~
CLEAR_IDS_MAP

COPY ~%MOD_FOLDER%/animations/displacer_beast/exxx.ini~ ~override/%hexNumber%.ini~

ACTION_BASH_FOR ~%MOD_FOLDER%/animations/displacer_beast~ ~^.+.bam$~ BEGIN
  COPY ~%BASH_FOR_FILESPEC%~ ~override~
END

// Create 5 unique instances of displacer beasts
ACTION_FOR_EACH displacer IN 
        1
        2
        3
        4
        5
    BEGIN
        COPY ~%MOD_FOLDER%/creatures/displacer/modisp01.cre~ ~override/modisp0%displacer%.cre~  // Displacer Beast
          SAY NAME1 @15008
          SAY NAME2 @15008
          WRITE_LONG 0x28 (IDS_OF_SYMBOL (~animate~ ~AC#DISPLACER_BEAST~))
          WRITE_ASCII 0x258 ~modisp01~           #8 // Add Race Script
          WRITE_ASCII 0x268 ~momatk01~           #8 // Add Default Script
          WRITE_ASCII 0x280 ~modisp0%displacer%~ #8 // Script Name
          REPLACE_CRE_ITEM ~modisp01~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON1~ EQUIP
END

COPY ~%MOD_FOLDER%/creatures/displacer/modisp01.itm~ ~override~  // Claws


COPY ~%MOD_FOLDER%/creatures/mutboalisk~ ~override~
// Create 2 unique instances of boalisk
ACTION_FOR_EACH boa IN 
        1
        2
    BEGIN
        COPY_EXISTING ~ohbmena5.cre~ ~override/mosnek0%boa%.cre~
            SAY 0x08 @15009
            SAY 0x0c @15009
            WRITE_BYTE 0x53   8  // 5/2 APR Base
            REMOVE_CRE_ITEM minhp1
            WRITE_ASCII SCRIPT_OVERRIDE ~ohbmeng~
            WRITE_ASCII 0x250 ~~             #8 // Clear Class Script
            WRITE_ASCII 0x258 ~mosnek01~     #8 // Add Race Script
            WRITE_ASCII 0x268 ~momatk01~     #8 // Add Default Script
            WRITE_ASCII 0x280 ~mosnek0%boa%~ #8 // Script Name
END

COPY ~%MOD_FOLDER%/creatures/mutcarrion~ ~override~
// Create 3 unique instances of carrion crawlers
ACTION_FOR_EACH crawl IN 
        1
        2
        3
    BEGIN
        COPY_EXISTING ~ohbmena6.cre~ ~override/mocrwl0%crawl%.cre~
            SAY 0x08 @15010
            SAY 0x0c @15010
            REMOVE_CRE_ITEM minhp1
            WRITE_ASCII SCRIPT_OVERRIDE ~ohbmeng~
            WRITE_ASCII 0x250 ~~               #8 // Clear Class Script
            WRITE_ASCII 0x258 ~mocrwl01~       #8 // Add Race Script
            WRITE_ASCII 0x268 ~momatk01~       #8 // Add Default Script
            WRITE_ASCII 0x280 ~mocrwl0%crawl%~ #8 // Script Name
END

COPY ~%MOD_FOLDER%/creatures/mutvortex~ ~override~
// Create 2 unique instances of vortex spiders
ACTION_FOR_EACH spdr IN 
        1
        2
    BEGIN
        COPY_EXISTING ~ohbmena7.cre~ ~override/mospdr0%spdr%.cre~
            SAY 0x08 @15011
            SAY 0x0c @15011
            WRITE_BYTE 0x53   2  // 2 APR Base
            REMOVE_CRE_ITEM minhp1
            WRITE_ASCII SCRIPT_OVERRIDE ~ohbmeng~
            WRITE_ASCII 0x250 ~~         #8 // Clear Class Script
            WRITE_ASCII 0x258 ~mospdr02~ #8 // Add Race Script
            WRITE_ASCII 0x268 ~momatk01~ #8 // Add Default Script
            WRITE_ASCII 0x280 ~mospdr0%spdr%~ #8 // Script Name
            LPF ADD_CRE_EFFECT INT_VAR opcode = 176 target = 0 parameter1 = 5 parameter2 = 0 timing = 9 resist_dispel = 0 probability1 = 100 END  // movement speed bonus
END

COPY ~%MOD_FOLDER%/creatures/mutgwyvern~ ~override~
// Create 2 unique instances of wyverns
ACTION_FOR_EACH wvrn IN 
        1
        2
    BEGIN
        COPY_EXISTING ~ohbmena8.cre~ ~override/mowvrn0%wvrn%.cre~
            SAY 0x08 @15012
            SAY 0x0c @15012
            WRITE_BYTE 0x24 120  // Current HP
            WRITE_BYTE 0x26 120  // Maximum HP
            WRITE_BYTE 0x60  20  // Slashing resistance
            WRITE_BYTE 0x61  20  // Crushing resistance
            WRITE_BYTE 0x62  20  // Piercing resistance
            WRITE_BYTE 0x63  20  // Missile resistance
            REMOVE_CRE_ITEM minhp1
            WRITE_ASCII SCRIPT_OVERRIDE ~ohbmeng~
            WRITE_ASCII 0x250 ~~         #8 // Clear Class Script
            WRITE_ASCII 0x258 ~mowvrn01~ #8 // Add Race Script
            WRITE_ASCII 0x268 ~momatk01~ #8 // Add Default Script
            WRITE_ASCII 0x280 ~mowvrn0%wvrn%~ #8 // Script Name
END

COPY ~%MOD_FOLDER%/creatures/brodle~ ~override~
COPY_EXISTING ~ohbbrodl.cre~ ~override~
    SAY 0x08 @15013
    SAY 0x0c @15013

COPY ~%MOD_FOLDER%/creatures/voghiln~ ~override~
COPY_EXISTING_REGEXP ~ohbvog0[2-5]\.cre~ ~override~
    WRITE_ASCII 0x34 "zbvoghil"
    WRITE_ASCII 0x3c "zbvoghil"
COPY_EXISTING ~ohbvoghi.cre~ ~override~
              ~ohnvoghi.cre~ ~override~
              ~ohbvogff.cre~ ~override~
    WRITE_ASCII 0x34 "zbvoghil"
    WRITE_ASCII 0x3c "zbvoghil"

COPY ~%MOD_FOLDER%/creatures/bellowgulp~ ~override~
COPY_EXISTING ~ohbbello.cre~ ~override~
    SAY 0x08 @15015
    SAY 0x0c @15015

COPY ~%MOD_FOLDER%/creatures/tartle~ ~override~
COPY_EXISTING ~ohbtartl.cre~ ~override~
    SAY 0x08 @15016
    SAY 0x0c @15016

COPY ~%MOD_FOLDER%/creatures/szasstam~ ~override~
COPY_EXISTING ~ohbszass.cre~ ~override~
    SAY 0x08 @15017
    SAY 0x0c @15017

COPY ~%MOD_FOLDER%/creatures/match1orcs~ ~override~


COPY ~%MOD_FOLDER%/creatures/match2mino~ ~override~
COPY_EXISTING ~ohbmin01.cre~ ~override~
    SAY 0x08 @15018
    SAY 0x0c @15018

// ohb_102 Thief fight
COPY_EXISTING ~OHBGLA01.cre~ ~override~
    WRITE_ASCII 0x260 ~motatk01~ #8 // General Script (theif attacks)
    WRITE_ASCII 0x268 ~momatk01~ #8 // Add Default Script (generic melee)

// ohb_102 Gladiator Archers
COPY_EXISTING ~ohbgla02.cre~ ~override~
    SET_BG2_PROFICIENCY ~PROFICIENCYLONGBOW~ 5

// minotaurs gore attack
COPY ~%MOD_FOLDER%/creatures/match2mino~ ~override~
COPY_EXISTING ~ohbmin01.cre~ ~override~
    LPF ADD_CRE_EFFECT INT_VAR opcode = 248 timing = 9 STR_VAR resource = ~ZBGOREM0~ END

COPY_EXISTING ~ZBGOREM1.SPL~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF(@18004) END // add .tra ref later

	COPY ~%MOD_FOLDER%/creatures/thewinged~ ~override~
COPY_EXISTING ~ohbwing.cre~ ~override~
    SAY 0x08 @15019
    SAY 0x0c @15019
 
// Add Ranged Scripts & APR Boost
ACTION_FOR_EACH ranged IN 
        ohborc02 // orc archer
        ohbgla02 // gladiator
    BEGIN
        ACTION_IF FILE_EXISTS_IN_GAME ~%ranged%.cre~ BEGIN
            COPY_EXISTING ~%ranged%.cre~ ~override~
                WRITE_BYTE  0x53   7            // 3/2 APR Base
                WRITE_ASCII 0x268 ~moratk01~ #8 // Add Default Script
        END
    END

// Add Ranged Scripts & APR Boost (Drow)
ACTION_FOR_EACH ranged1 IN 
        ohbdro02 // drow scout
    BEGIN
        ACTION_IF FILE_EXISTS_IN_GAME ~%ranged1%.cre~ BEGIN
            COPY_EXISTING ~%ranged1%.cre~ ~override~
                WRITE_BYTE  0x53   2            // 3/2 APR Base
                WRITE_ASCII 0x268 ~moratk01~ #8 // Add Default Script
        END
    END

// Add Melee Scrips & APR Boost
ACTION_FOR_EACH melee IN 
        ohborc01 // orc
        ohbmin01 // minotaur
        ohbliz02 // lizard man elite
        ohbliz01 // lizard man chieftain
    BEGIN
        ACTION_IF FILE_EXISTS_IN_GAME ~%melee%.cre~ BEGIN
            COPY_EXISTING ~%melee%.cre~ ~override~
                WRITE_BYTE  0x53   7            // 3/2 APR Base
                WRITE_ASCII 0x268 ~momatk01~ #8 // Add Default Script
        END
    END

// Add Melee Scrips & APR Boost (Drow)
ACTION_FOR_EACH melee1 IN 
        ohbdro01 // drow warrior
    BEGIN
        ACTION_IF FILE_EXISTS_IN_GAME ~%melee1%.cre~ BEGIN
            COPY_EXISTING ~%melee1%.cre~ ~override~
                WRITE_BYTE  0x53   2            // 2 APR Base
                WRITE_ASCII 0x268 ~momatk01~ #8 // Add Default Script
        END
    END

// ohb_104 Elder Umber Hulk fight
COPY_EXISTING ~ohbumb01.CRE~ ~override~ 
    WRITE_BYTE 0x24 110  // Current HP
    WRITE_BYTE 0x26 110  // Maximum HP

///////////////////////////////////////////////////////////////////////////
// Copy .BCS file to override folder                                     //
///////////////////////////////////////////////////////////////////////////
COPY ~%MOD_FOLDER%/creatures/#scripts/momatk01.baf~ ~override/momatk01.bcs~
     ~%MOD_FOLDER%/creatures/#scripts/moratk01.baf~ ~override/moratk01.bcs~
     ~%MOD_FOLDER%/creatures/#scripts/modisp01.baf~ ~override/modisp01.bcs~
     ~%MOD_FOLDER%/creatures/#scripts/mospdr02.baf~ ~override/mospdr02.bcs~
     ~%MOD_FOLDER%/creatures/#scripts/mosnek01.baf~ ~override/mosnek01.bcs~
     ~%MOD_FOLDER%/creatures/#scripts/mocrwl01.baf~ ~override/mocrwl01.bcs~
     ~%MOD_FOLDER%/creatures/#scripts/mowvrn01.baf~ ~override/mowvrn01.bcs~
     ~%MOD_FOLDER%/creatures/#scripts/mobear01.baf~ ~override/mobear01.bcs~
     ~%MOD_FOLDER%/creatures/#scripts/ohbumb01.baf~ ~override/ohbumb01.bcs~
     ~%MOD_FOLDER%/creatures/#scripts/zbdom.baf~    ~override/zbdom.bcs~
  COMPILE_BAF_TO_BCS